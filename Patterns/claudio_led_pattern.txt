/////////////////////////////////////////////////////////////
//
// claudio_led_pattern.txt
//
/////////////////////////////////////////////////////////////
// compiler_version:v2.3 PLEASE DO NOT REMOVE OR ALTER THIS LINE
/////////////////////////////////////////////////////////////

// My tarot has 11 LED on each of the 4 arms
define NAV_BEGIN             1
define NAV_END               3
define FLASHERS_BEGIN        5				
define FLASHERS_END          11
define STATUS_BEGIN          11             
define STATUS_END            11
define MAX_LEDS_PER_STRIP    11



// Define group numbers

define ALL_LEDS         1
define STRIP_NUMBER_ID  3

define FRONT            3
define BACK             4
define MIDDLE_FOUR      5

define LEFT             6
define RIGHT            7

define FRONT_LEFT       8
define FRONT_RIGHT      9
define BACK_LEFT       10
define BACK_RIGHT      11

define NAV_LEFT        12
define NAV_RIGHT       13
define FLASHERS        14
define STATUS          15

define FL_FL           16
define FL_FR           17
define FL_BL           18
define FL_BR           19

define ALL_LEDS1       20


// Define standard colors

define RED            [255 0 0]
define GREEN          [0 255 0]
define BLUE           [0 0 255]
define YELLOW         [255 255 0]
define CYAN           [0 255 255]
define MAGENTA        [255 0 255]
define ORANGE         [255 128 0]

define OFF            [0 0 0]	         
       
define WHITE_20       [50 50 50]	             // ~20% White	
define WHITE_80       [200 200 200]	            // ~80% White	
define WHITE_100      [255 255 255]              // 100% White
define RED_20         [50 0 0]                  // ~20% Red

initialize begin

	// group ALL_LEDS is used for all leds - usually this group is used for background
	append group ALL_LEDS as strip 1 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS as strip 2 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS as strip 3 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS as strip 4 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS as strip 5 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS as strip 6 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS as strip 7 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS as strip 8 led 1 to MAX_LEDS_PER_STRIP

	// group ALL_LEDS is used for all leds - usually this group is used for background
	append group ALL_LEDS1 as strip 1 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS1 as strip 2 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS1 as strip 3 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS1 as strip 4 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS1 as strip 5 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS1 as strip 6 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS1 as strip 7 led 1 to MAX_LEDS_PER_STRIP
	append group ALL_LEDS1 as strip 8 led 1 to MAX_LEDS_PER_STRIP

    // group for strip number identification
	append group STRIP_NUMBER_ID as strip 1 led 1 to 1
	append group STRIP_NUMBER_ID as strip 2 led 1 to 2
	append group STRIP_NUMBER_ID as strip 3 led 1 to 3
	append group STRIP_NUMBER_ID as strip 4 led 1 to 4
	append group STRIP_NUMBER_ID as strip 5 led 1 to 5
	append group STRIP_NUMBER_ID as strip 6 led 1 to 6
	append group STRIP_NUMBER_ID as strip 7 led 1 to 7
	append group STRIP_NUMBER_ID as strip 8 led 1 to 8


	// groups for front, back and middle
	append group FRONT as strip 1 led 1 to MAX_LEDS_PER_STRIP
	append group FRONT as strip 2 led 1 to MAX_LEDS_PER_STRIP
	append group BACK as strip 7 led 1 to MAX_LEDS_PER_STRIP
	append group BACK as strip 8 led 1 to MAX_LEDS_PER_STRIP
	append group MIDDLE_FOUR as strip 3 led 1 to MAX_LEDS_PER_STRIP
	append group MIDDLE_FOUR as strip 4 led 1 to MAX_LEDS_PER_STRIP
	append group MIDDLE_FOUR as strip 5 led 1 to MAX_LEDS_PER_STRIP
	append group MIDDLE_FOUR as strip 6 led 1 to MAX_LEDS_PER_STRIP  
  
    // groups for left and right
	append group LEFT as strip 1 led 1 to MAX_LEDS_PER_STRIP        
	append group LEFT as strip 3 led 1 to MAX_LEDS_PER_STRIP        
	append group LEFT as strip 5 led 1 to MAX_LEDS_PER_STRIP         
	append group LEFT as strip 7 led 1 to MAX_LEDS_PER_STRIP         
	append group RIGHT as strip 2 led 1 to MAX_LEDS_PER_STRIP        
	append group RIGHT as strip 4 led 1 to MAX_LEDS_PER_STRIP        
	append group RIGHT as strip 6 led 1 to MAX_LEDS_PER_STRIP        
	append group RIGHT as strip 8 led 1 to MAX_LEDS_PER_STRIP        
	   
    // groups for corner arms
	append group FRONT_LEFT  as strip 1 led 1 to MAX_LEDS_PER_STRIP
	append group FRONT_RIGHT as strip 2 led 1 to MAX_LEDS_PER_STRIP
	append group BACK_LEFT   as strip 7 led 1 to MAX_LEDS_PER_STRIP
	append group BACK_RIGHT  as strip 8 led 1 to MAX_LEDS_PER_STRIP

    // groups for navigation lights
  	append group NAV_LEFT  as strip 1 led 1 to NAV_END
	append group NAV_LEFT  as strip 7 led 1 to NAV_END
    append group NAV_RIGHT as strip 2 led 1 to NAV_END
	append group NAV_RIGHT as strip 8 led 1 to NAV_END

    // groups for all flashers lights
    append group FLASHERS as strip 1 led 4 to FLASHERS_END
    append group FLASHERS as strip 2 led 4 to FLASHERS_END
    append group FLASHERS as strip 3 led 4 to FLASHERS_END
    append group FLASHERS as strip 4 led 4 to FLASHERS_END
    append group FLASHERS as strip 5 led 4 to FLASHERS_END
    append group FLASHERS as strip 6 led 4 to FLASHERS_END
    append group FLASHERS as strip 7 led 4 to FLASHERS_END
    append group FLASHERS as strip 8 led 4 to FLASHERS_END
    
    // groups for individual arm flashers lights for waves and bounces
    append group FL_FL as strip 1 led 4 to FLASHERS_END
    append group FL_FR as strip 2 led 4 to FLASHERS_END
    append group FL_BL as strip 7 led 4 to FLASHERS_END
    append group FL_BR as strip 8 led 4 to FLASHERS_END

    // groups for status lights
    append group STATUS as strip 1 led 10 to MAX_LEDS_PER_STRIP
    append group STATUS as strip 2 led 10 to MAX_LEDS_PER_STRIP
    append group STATUS as strip 3 led 10 to MAX_LEDS_PER_STRIP
    append group STATUS as strip 4 led 10 to MAX_LEDS_PER_STRIP
    append group STATUS as strip 5 led 10 to MAX_LEDS_PER_STRIP
    append group STATUS as strip 6 led 10 to MAX_LEDS_PER_STRIP
    append group STATUS as strip 7 led 10 to MAX_LEDS_PER_STRIP
    append group STATUS as strip 8 led 10 to MAX_LEDS_PER_STRIP

end

/////////////////////////////////////////////////////////////////////////////////////////////////////
//
// Pot on channel 8 can turn off LEDS or run automatic patterns based on flight modes. 
// Every mode has red/green navigation leds toward the outer part of the strip
// then there is a "flasher" region that displys a typical pattern/color that is mode specific
// If there is a warning condition it will be displayed by the inner 2 "status" LEDS
// If there is a critical condition it will be displayed prominently by the flashers
//
//////////////////////////////////////////////////////////////////////////////////////////////////////

// Transmitter Off or pot low, show test pattern

if ( rc.ch8 <= 1100 ) then 
	color group ALL_LEDS WHITE_20									 
	color group STRIP_NUMBER_ID RED_20							 
end

// default navigation mode 

if ( 1100 < rc.ch8 and rc.ch8 <= 1200  ) then
    ////////////////////////////////////////////////////////////////////////////////////////////////
    //
	// We are disarmed, controller is aquiring GPS and doing checks
	// will show gps status: red -> bad; orange -> fair; nav lights on-> good to go
	//
	////////////////////////////////////////////////////////////////////////////////////////////////
    if ( fc.armed==0 ) then
    	color group ALL_LEDS OFF
		if (gps.hdop <= 250) then				           // good, ready to go show airworthy ship
		    color group NAV_LEFT RED
		    color group NAV_RIGHT GREEN        
		    bounce group FL_FL  WHITE_80 100 1                 
	        bounce group FL_FR  WHITE_80 100 1               
		    bounce group FL_BL  WHITE_80 100 1              
		    bounce group FL_BR  WHITE_80 100 1   				        	
		end
	    if (250 < gps.hdop and gps.hdop <= 500) then        // fair
			bounce group FRONT_LEFT  ORANGE    100 1                 
		    bounce group FRONT_RIGHT ORANGE 100 1               
		    bounce group BACK_LEFT   ORANGE     100 1              
		    bounce group BACK_RIGHT  ORANGE  100 1   				        	  
		end
    	if ( 500 < gps.hdop ) then						   	//poor
			bounce group FRONT_LEFT  RED    100 1                 
		    bounce group FRONT_RIGHT RED 100 1               
		    bounce group BACK_LEFT   RED     100 1              
		    bounce group BACK_RIGHT  RED  100 1   	
		end			        
	end

	////////////////////////////////////////////////////////////////////////////////////////////////
    //
	// We are armed. Will display patterns based on flight modes
	// will also display warnings and critical conditions
	//
	////////////////////////////////////////////////////////////////////////////////////////////////
	if (fc.armed == 1) then
	   color group NAV_LEFT RED
	   color group NAV_RIGHT GREEN
	   if ( fc.flightmode == 0 ) then           // Stabilize
	       color group FLASHERS OFF	
	       flash group FLASHERS YELLOW 30 970 0
	       flash group FLASHERS YELLOW 20 980 200				 
		end

	   if ( fc.flightmode == 2 ) then           // Alt Hold
	       color group FLASHERS OFF	
	       flash group FLASHERS WHITE_80 30 970 0
	       flash group FLASHERS WHITE_80 20 980 200				 
		end

		if ( fc.flightmode == 6 ) then           // RTL
			color group FLASHERS OFF						 
		    bounce group FL_FL  BLUE 100 1                 
	        bounce group FL_FR  BLUE 100 1               
		    bounce group FL_BL  BLUE 100 1              
		    bounce group FL_BR  BLUE 100 1   				        	
		end  

		if ( fc.flightmode == 16 ) then          // Position Hold
			color group FLASHERS BLUE					 
	        flash group FLASHERS WHITE_80 30 970 0
	        flash group FLASHERS WHITE_80 20 980 200				 
		end
  
		if ( fc.flightmode == 9 ) then           // Land
			color group FLASHERS BLUE						 
		    bounce group FL_FL  WHITE_80 100 1                 
	        bounce group FL_FR  WHITE_80 100 1               
		    bounce group FL_BL  WHITE_80 100 1              
		    bounce group FL_BR  WHITE_80 100 1   				        	
		end  
		/////////////////////////////////////////////////////////////
		// Warnings and Criticals
		/////////////////////////////////////////////////////////////
		if ( gps.hdop > 500 ) then                // Bad HDOP
			color group STATUS OFF									 
			flash group STATUS BLUE 500 500 0							 
		end     
		   
		if ( bat.voltage < 110  ) then         // Battery < 11.0 Volts flash yellow/red
			color group STATUS RED									 
			flash group STATUS YELLOW 500 500 0							 
		end

		if ( bat.voltage < 177  ) then         // Battery < 10.7 Volts flash red
			color group FLASHERS OFF								 
			flash group FLASHERS RED 200 800 0							 
		end

		//if ( fc.rssi <100 ) then	              // RSSI log going too far
		//    color group NAV_LEFT OFF
		//    color group NAV_RIGHT OFF
		//    flash group NAV_LEFT RED 500 500 0
		//    flash group NAV RIGHT GREEN 500 500 500
		//end
	end
 end
  
if ( 1200 < rc.ch8 and rc.ch8 <= 1300  ) then 
	color group ALL_LEDS1 OFF					 
	bar group FRONT_LEFT RED    25 0 100 1
    bar group FRONT_RIGHT GREEN 25 0 100 1     
	bar group BACK_LEFT RED     25 0 100 1     
	bar group BACK_RIGHT GREEN  25 0 100 1   		
end
  
if ( 1300 < rc.ch8 and rc.ch8 <= 1400 ) then       // scotts nav pattern
	color group ALL_LEDS1 OFF						 
	fill group FRONT_LEFT RED    150 1000 1
	fill group FRONT_RIGHT GREEN 150 1000 1
	fill group BACK_LEFT RED     150 1000 0
	fill group BACK_RIGHT GREEN  150 1000 0
end
  
if ( 1400 < rc.ch8 and rc.ch8 <= 1500 ) then
	color group ALL_LEDS1 OFF						 
	wave group FRONT_LEFT RED    50 2	1
	wave group FRONT_RIGHT GREEN 50 2 1
	wave group BACK_LEFT RED     50 2 0
	wave group BACK_RIGHT GREEN  50 2 0			  
end

if ( 1500 < rc.ch8 and rc.ch8 <= 1600 ) then 
	color group ALL_LEDS #050505		
	bounce group FRONT_LEFT RED    100 1                 
	bounce group FRONT_RIGHT GREEN 100 1               
	bounce group BACK_LEFT RED     100 1              
	bounce group BACK_RIGHT GREEN  100 1   				        
end
  
if ( 1600 < rc.ch8 and rc.ch8 <= 1700 ) then
	color group  ALL_LEDS1 OFF						 
	flash group LEFT RED    20 950   0     
	flash group LEFT RED    20 950 100       
	flash group RIGHT GREEN 20 950 200        
	flash group RIGHT GREEN 20 950 300   							
end
  
if ( 1700 < rc.ch8 and rc.ch8 <= 1800 ) then   
        color group FRONT WHITE_20                       // Front white					 
        color group BACK RED_20                          // Back low red		
        if ( imu.brake > 20 ) then                       // Braking
           color group BACK RED                          // Back bright red		 
        end  
end
  
if ( 1800 < rc.ch8 and rc.ch8 <= 1900 ) then 
	color group ALL_LEDS1 OFF			            // off	
end  
  
if ( 1900 < rc.ch8 ) then 
	color group ALL_LEDS1 WHITE_80					
	bar group FRONT_LEFT RED    25 0 100  1   
    bar group FRONT_RIGHT GREEN 25 0 100  1     
	bar group BACK_LEFT RED     25 0 100  1     
	bar group BACK_RIGHT GREEN  25 0 100  1   	  
end